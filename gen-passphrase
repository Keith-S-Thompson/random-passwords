#!/usr/bin/perl

use strict;
use warnings;

use File::Spec ();
use Getopt::Long ();
use Math::BigInt ();
use Math::BigFloat ();

my $Program_Name = (File::Spec->splitpath($0))[2];

my $default_dictionary = '/usr/share/dict/words';

my %Opt = ( dictionary => defined $ENV{PASSPHRASE_DICT}
                                ? $ENV{PASSPHRASE_DICT}
                                : $default_dictionary );
my @Opts = ( \%Opt, qw( help!
                        verbose!
                        dictionary=s
                        dev-random!
                        debugging! ) );

Getopt::Long::GetOptions @Opts or Usage();
Usage() if $Opt{help};

Usage() unless scalar @ARGV == 3 and
               ($ARGV[0] =~ /^[a-z]+$/ or $ARGV[0] =~ /^\d+$/) and
               $ARGV[1] =~ /^\d+$/ and
               $ARGV[2] =~ /^\d+$/;

my($selector, $min_len, $max_len) = @ARGV;
Usage("max_len must be >= min_len\n") if $max_len < $min_len;

my $use_initials = ($selector =~ /^[a-z]+$/);

my @words = ();

#
# Slurp all lowercase words from the dictionary file into an array.
#
open my $DICT, '<', $Opt{dictionary} or die "$Opt{dictionary}: $!\n";
while (<$DICT>) {
    chomp;
    s/\r$//;
    next if not /^[a-z]+$/;
    push @words, $_;
}
close $DICT;

# print scalar @words, " words\n";

my @passphrase = ();

my @counts = ();
my @errors = ();

my $stats = '';

#
# Generate the passphrase
#
if ($use_initials) {
    #
    # The first program argument gives the initials of the generated
    # passphrase.
    #
    foreach my $initial (split //, $selector) {
        my @candidates = Words($initial, $min_len, $max_len);
        my $s = scalar @candidates == 1 ? '' : 's';
        if (@candidates) {
            push @counts, scalar @candidates;
            my $index = Random(scalar @candidates);
            push @passphrase, $candidates[$index];

        }
        else {
            my $len = $min_len;
            $len .= "..$max_len" if $max_len > $min_len;
            push @errors, "No words starting with '$initial' with $len letters\n";
        }
    }
}
else {
    #
    # The first program argument gives the number of words.
    #
    my @candidates = Words(undef, $min_len, $max_len);
    my $s = scalar @candidates == 1 ? '' : 's';
    @counts = (scalar @candidates) x $selector;
    if (@candidates) {
        for (1 .. $selector) {
            my $index = Random(scalar @candidates);
            push @passphrase, $candidates[$index];
        }
    }
    else {
        my $len = $min_len;
        $len .= "..$max_len" if $max_len > $min_len;
        push @errors, "No words with $len letters\n";
    }
}

if (@errors) {
    die @errors;
}

print "@passphrase\n";
if ($Opt{verbose}) {
    if ($use_initials) {
        $stats .= "    " . join(' * ', @counts) . "\n";
    }
    else {
        $stats .= "    " . $counts[0] . "**" . $selector . "\n";
    }
    my $possibilities = Math::BigInt->new(1);
    foreach my $count (@counts) {
        $possibilities *= $count;
    }
    my $f_possibilities = Math::BigFloat->new($possibilities);
    $stats .= "    " . Image($possibilities) . " possibilities, equivalent to:\n";
    my $log_possibilities = log($f_possibilities);
    $stats .= sprintf "    %.2f random bits\n" .
                      "    %.2f random lowercase letters\n" .
                      "    %.2f random mixed-case alphanumerics\n" .
                      "    %.2f random printable ASCII characters\n",
                      $log_possibilities / log(2),
                      $log_possibilities / log(26), # a-z
                      $log_possibilities / log(62), # a-zA-Z0-9
                      $log_possibilities / log(94); # !-~
    print $stats;
}

########################################################################

#
# Random($upper) returns a random number in the range 0 .. $upper-1
#
sub Random {
    my($upper) = @_;
    my $buf;
    my $short_bound = 2**16;
    my $value_bound = $short_bound - ($short_bound % $upper);
    my $value;

    # Values read from the device will be in the range 0 .. $short_bound-1
    # (0..65535).  Repeat if necessary to get a value in the range
    # 0..$value_bound-1, which is computed to avoid bias; the number of possible
    # values read is a multiple of $upper.

    my $device = $Opt{'dev-random'} ? '/dev/random' : '/dev/urandom';
    Debug("Reading from $device\n");
    open my $RANDOM, $device or die "${device}: $!\n";

    #
    # Read 2 bytes from device.
    # Discard high values to avoid bias.
    #
    do {
        my $bytes_read = read $RANDOM, $buf, 2;
        die "Failed to read from $device\n" if $bytes_read != 2;
        $value = unpack 'S', $buf;
        Debugf("Read value 0x%04x\n", $value);
    } while $value >= $value_bound;

    close $RANDOM;

    my $result = $value % $upper;
    Debugf("Random: 0x%04x %% %d --> %d\n",
           $value, $upper, $result);
    return $result;
} # Random

# ----------------------------------------------------------------------

#
# Returns a list of words satisfying the specified criteria
#
sub Words {
    my($initial, $min_len, $max_len) = @_;
    return grep { (defined $initial ? substr($_, 0, 1) eq $initial : 1) and
                  length $_ >= $min_len and
                  length $_ <= $max_len } @words;
}

# ----------------------------------------------------------------------

#
# Insert commas into a string, e.g., "1234567" --> "1,234,567"
#
sub Comma {
    my($num) = sprintf "%.0f", $_[0];
    $num = reverse $num;
    $num =~ s/.../$&,/g;
    $num = reverse $num;
    $num =~ s/^,//;
    return $num;
}

# ----------------------------------------------------------------------

sub Image {
    my($num) = @_;
    if ($num < 10000) {
        return $num;
    }
    elsif ($num < 1e9) {
        return Comma $num;
    }
    else {
        $num = Math::BigFloat->new($num);
        my $exp = int($num->copy()->blog(10));
        my $digits = substr($num->bstr(), 0, 5);
        $digits =~ s/^./$&./;
        return "${digits}e$exp";
    }
}

# ----------------------------------------------------------------------

sub Usage {
    print @_ if @_;
    print <<"EOF";
Usage: $Program_Name [options] initials   min-len max-len
       $Program_Name [options] word-count min-len max-len
    -help             Show this message and exit
    -verbose          Show statistics about the strength of the passphrase
    -dictionary file  Use specified word list
                      Default is $default_dictionary or \$PASSPHRASE_DICT
    -dev-random       Use /dev/random rather than /dev/urandom (slow)
    -debugging        Produce debugging output
Option names may be abbreviated; for example, "-verbose" may be given as "-v".
The passphrase consists of a sequence of words randomly selected
from the specified word list file.
The first argument is either a string of lowercase letters
(specifying the initial letters of the generated passphrase) or a
decimal integer specifying the number of words.
"min-len" and "max-len" are decimal integers determine the lengths
of the chosen words
EOF
    exit 1;
} # Usage

# ----------------------------------------------------------------------

sub Debug {
    print @_ if $Opt{debugging};
} # Debug

# ----------------------------------------------------------------------

sub Debugf {
    printf @_ if $Opt{debugging};
} # Debugf
